subprojects {
  apply plugin: 'java'

  version=project.properties['revision'] ?: '1.7-SNAPSHOT'

  task libsProd(type: Sync) {
    from configurations.compile
    into "build/lib"
  }

  task libsTest(type: Sync) {
    from configurations.testRuntime
    into "build/lib-test"
  }

  compileJava.dependsOn libsProd, libsTest

  repositories {
    jcenter()
    mavenLocal()
  }

  compileJava {
    options.debugOptions.debugLevel = "source,lines,vars"
    options.fork = true
    options.compilerArgs << '-parameters'
    options.encoding = 'UTF-8'
  }

  sourceSets {
    main {
      java {srcDir "app"}
      resources {srcDirs "conf", "app"}
    }
    test {
      java {srcDir "test"}
      resources {srcDir "test"}
    }
  }

  dependencies {
    compile("com.codeborne.replay:framework:${version}")
    compile("com.codeborne.replay:fastergt:${version}")

    testCompile('org.assertj:assertj-core:3.15.0')
    testCompile('com.codeborne:selenide:5.7.0')
    testCompile('junit:junit:4.13')
    testCompile('org.mockito:mockito-core:3.2.4')
  }

  test {
    exclude 'ui/**'
  }

  task uitest(type: Test) {
    include 'ui/**/*'
    systemProperties['BUILD_URL'] = System.getenv()['BUILD_URL']
  }

  tasks.withType(Test).all { testTask ->
    testTask.systemProperties['file.encoding'] = 'UTF-8'
    testTask.testLogging {exceptionFormat = 'full'}
    testTask.outputs.upToDateWhen { false }
  }
}
