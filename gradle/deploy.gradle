subprojects {
  jar {
    from sourceSets.main.allSource

    manifest {
      attributes(
        "Automatic-Module-Name": project.group + '.' + project.name,
        "Implementation-Title": project.group + '.' + project.name,
        "Implementation-Version": version,
        "Implementation-Vendor": "Codeborne")
    }
  }

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  javadoc {
    failOnError = false
    source = sourceSets.main.allJava
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
  }


  if (project.hasProperty("signing.keyId")) {
    apply plugin: 'signing'

    signing {
      sign configurations.archives
    }

    uploadArchives {
      repositories {
        mavenDeployer {
          beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

          repository(url: project.version.endsWith("-SNAPSHOT") ?
            'https://oss.sonatype.org/content/repositories/snapshots/' :
            'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: "$sonatypeUsername", password: "$sonatypePassword")
          }

          pom.project {
            name archivesBaseName
            packaging 'jar'
            description 'RePlay is a fork of the Play1 framework, made and maintained by Codeborne.'
            url 'https://github.com/codeborne/replay'

            scm {
              url 'scm:git@github.com:codeborne/replay.git'
              connection 'scm:git@github.com:codeborne/replay.git'
              developerConnection 'scm:git@github.com:codeborne/replay.git'
            }

            licenses {
              license {
                name 'MIT'
                url 'https://opensource.org/licenses/MIT'
                distribution 'repo'
              }
            }

            developers {
              developer {
                id 'asolntsev'
                name 'Andrei Solntsev'
              }
            }
          }

          //mess with the generated pom to set the 'packaging' tag
          pom.withXml { XmlProvider xmlProvider ->
            def xml = xmlProvider.asString()
            def pomXml = new XmlParser().parse(new ByteArrayInputStream(xml.toString().bytes))

            pomXml.version[0] + { packaging('jar') }

            def newXml = new StringWriter()
            def printer = new XmlNodePrinter(new PrintWriter(newXml))
            printer.preserveWhitespace = true
            printer.print(pomXml)
            xml.setLength(0)
            xml.append(newXml.toString())
          }
        }
      }
    }
  }
}