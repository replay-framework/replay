apply plugin: 'maven-publish'

jar {
  manifest {
    attributes(
        "Automatic-Module-Name": project.group + '.' + project.name,
        "Implementation-Title": project.group + '.' + project.name,
        "Implementation-Version": archiveVersion,
        "Implementation-Vendor": "https://github.com/replay-framework")
  }
}

tasks.register('sourcesJar', Jar) {
  dependsOn classes
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

tasks.withType(Javadoc).configureEach {
  failOnError = false
  options.encoding = 'UTF-8'
  options.addStringOption('Xdoclint:none', '-quiet')
  source = sourceSets.main.allJava
}

tasks.register('javadocJar', Jar) {
  dependsOn javadoc
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

if (project.hasProperty("signing.keyId")) {
  apply plugin: 'signing'

  signing {
    afterEvaluate {
      sign publishing.publications.mavenJava
    }
  }
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

publishing {
  if (project.hasProperty("sonatypeUsername")) {
    repositories {
      maven {
        name 'Maven'
        url project.version.endsWith("-SNAPSHOT") ?
          'https://s01.oss.sonatype.org/content/repositories/snapshots/' :
          'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
        credentials {
          username "$sonatypeUsername"
          password "$sonatypePassword"
        }
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      groupId "${project.group}"
      artifactId "${project.name}"

      from components.java
      artifact(sourcesJar)
      artifact(javadocJar)

      pom {
        name = archivesBaseName
        description = 'RePlay is a fork of the Play1 framework, created by Codeborne.'
        url = 'https://github.com/replay-framework/replay'
        licenses {
          license {
            name = 'MIT'
            url = 'https://opensource.org/licenses/MIT'
          }
        }
        developers {
          developer {
            id = 'asolntsev'
            name = 'Andrei Solntsev'
          }
        }
        scm {
          connection = 'scm:git@github.com:replay-framework/replay.git'
          developerConnection = 'scm:git@github.com:replay-framework/replay.git'
          url = 'https://github.com/replay-framework/replay'
        }
      }
    }
  }
}
