defaultTasks 'clean', 'test', 'install'

subprojects {

  group='com.codeborne.replay'
  version=project.properties['revision'] ?: '1.8.8'

  apply plugin: 'java'

  [compileJava, compileTestJava]*.options.collect {options ->
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
    options.debug = true
    options.debugOptions.debugLevel = "source,lines,vars"
  }
  sourceCompatibility = 11
  targetCompatibility = 11

  repositories {
    mavenCentral()
    mavenLocal()
  }

  sourceSets {
    main {
      java {srcDir 'src'}
      resources {srcDir 'src'}
    }
    test {
      java {srcDir 'test'}
      resources {srcDir 'test'}
    }
  }

  test {
    include 'play/**/*'
    systemProperties['file.encoding'] = 'UTF-8'
  }

  tasks.withType(Test).all { testTask ->
    testTask.systemProperties['file.encoding'] = 'UTF-8'
    testTask.testLogging {exceptionFormat = 'full'}
    testTask.outputs.upToDateWhen { false }
  }
}
