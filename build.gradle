defaultTasks 'clean', 'check', 'publishToMavenLocal'

ext {
  commonsLangVersion = '3.16.0'
  netty4Version = '4.1.112.Final'
  commonsTextVersion = '1.12.0'
  slf4jVersion = '2.0.16'
  assertjVersion = '3.26.3'
  mockitoVersion = '5.12.0'
  pdfTestVersion = '1.9.0'
  groovyVersion = '3.0.22'
  httpclientVersion = '4.5.14'
  restAssuredVersion = '5.5.0'
  junitVersion = '5.10.3'
}

subprojects {

  group='io.github.replay-framework'
  version=project.properties['revision'] ?: '2.6.0-RC1'

  apply plugin: 'java'
  apply plugin: 'java-library'

  [compileJava, compileTestJava]*.options.collect { options ->
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
    options.debug = true
    options.debugOptions.debugLevel = "source,lines,vars"
  }
  java.toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }

  repositories {
    mavenCentral()
    mavenLocal()
    maven {
      url "https://maven.repository.redhat.com/ga/"
    }
  }

  sourceSets {
    main {
      java {
        srcDir 'src'
        include '**/*.java'
        include '**/*.kt'
      }
      resources {
        srcDir 'src'
        exclude '**/*.java'
        exclude '**/*.kt'
      }
    }
    test {
      java {
        srcDir 'test'
        include '**/*.java'
        include '**/*.kt'
      }
      resources {
        srcDir 'test'
        exclude '**/*.java'
        exclude '**/*.kt'
      }
    }
  }


  test {
    include 'play/**/*'
  }

  tasks.withType(Test).configureEach { testTask ->
    useJUnitPlatform()
    testTask.systemProperties['file.encoding'] = 'UTF-8'
    testTask.systemProperties['user.country'] = 'TR'
    testTask.systemProperties['user.language'] = 'tr'
    testTask.testLogging {exceptionFormat = 'full'}
    testTask.outputs.upToDateWhen { false }
  }

  dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testImplementation("org.junit.jupiter:junit-jupiter-params:$junitVersion")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
    testImplementation("org.mockito:mockito-core:$mockitoVersion")
    testImplementation("org.assertj:assertj-core:$assertjVersion")
  }
}
