configurations {
  thirdParty
  implementation.extendsFrom thirdParty
}

test {
  include 'play/**/*'
  include 'org/**/*'
}

dependencies {
  implementation project(':framework')
  testImplementation(libs.mockitoCore)

  implementation(libs.commonsLang3)
  implementation(libs.flyingSaucerPdf)
  implementation(libs.xercesParser)

  implementation(libs.shaniParser)
  api(libs.yahp) { transitive = false }
}

task extractThirdPartyJars(type: Sync) {
  dependsOn configurations.thirdParty

  from {
    configurations.thirdParty.collect { zipTree(it) }
  }
  into "$buildDir/thirdParty/"
  exclude 'META-INF'
  exclude 'META-INF/**/*'
  exclude 'org/allcolor/yahp/cl/converter/CHtmlToPdfFlyingSaucerTransformer*'
}

jar.dependsOn extractThirdPartyJars

jar {
  from sourceSets.main.output + "$buildDir/thirdParty/"
}

javadoc {
  exclude 'org/allcolor/**'
}

apply from: rootProject.file('gradle/deploy.gradle')
