configurations {
  thirdParty
  compile.extendsFrom thirdParty
}

ext {
  FLYING_SOURCER_VERSION = '9.1.9'
  YAHP_VERSION = 1.3
}

sourceSets {
  main {
    java {srcDir 'src'}
    resources {srcDir 'src'}
  }
}

test {
  include 'play/**/*'
  include 'org/**/*'
  systemProperties['file.encoding'] = 'UTF-8'
}

dependencies {
  compile project(':framework')
  testCompile project(':framework').sourceSets.test.compileClasspath
  testCompile('org.mockito:mockito-core:4.1.0')

  thirdParty("org.xhtmlrenderer:flying-saucer-core:$FLYING_SOURCER_VERSION")
  compile("org.xhtmlrenderer:flying-saucer-pdf:$FLYING_SOURCER_VERSION") {
    exclude group: 'org.xhtmlrenderer', module: 'flying-saucer-core'
    exclude group: 'org.bouncycastle'
  }

  compile('com.google.code.maven-play-plugin.org.allcolor.shanidom:shani-parser:1.4.17-patched-yahp-1.3')
  compile("com.google.code.maven-play-plugin.org.allcolor.yahp:yahp:$YAHP_VERSION") {transitive = false}
  thirdParty("com.google.code.maven-play-plugin.org.allcolor.yahp:yahp-internal:$YAHP_VERSION") {transitive = false}
}

task extractThirdPartyJars(type: Sync) {
  dependsOn configurations.thirdParty

  from {
    configurations.thirdParty.collect { zipTree(it) }
  }
  into "$buildDir/thirdParty/"
  exclude 'org/xhtmlrenderer/swing/NaiveUserAgent*'
  exclude 'org/allcolor/yahp/cl/converter/CHtmlToPdfFlyingSaucerTransformer*'
}

jar.dependsOn extractThirdPartyJars

jar {
  from sourceSets.main.output + "$buildDir/thirdParty/"
}

apply from: rootProject.file('gradle/deploy.gradle')
