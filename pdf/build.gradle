configurations {
  thirdParty
  implementation.extendsFrom thirdParty
}

ext {
  FLYING_SOURCER_VERSION = '9.1.9'
  YAHP_VERSION = 1.3
}

test {
  include 'play/**/*'
  include 'org/**/*'
  systemProperties['file.encoding'] = 'UTF-8'
}

dependencies {
  implementation project(':framework')
  testImplementation project(':framework').sourceSets.test.compileClasspath
  testImplementation('org.mockito:mockito-core:4.2.0')

  thirdParty("org.xhtmlrenderer:flying-saucer-core:$FLYING_SOURCER_VERSION")
  implementation("org.xhtmlrenderer:flying-saucer-pdf:$FLYING_SOURCER_VERSION") {
    exclude group: 'org.xhtmlrenderer', module: 'flying-saucer-core'
    exclude group: 'org.bouncycastle'
  }

  implementation('com.google.code.maven-play-plugin.org.allcolor.shanidom:shani-parser:1.4.17-patched-yahp-1.3')
  api("com.google.code.maven-play-plugin.org.allcolor.yahp:yahp:$YAHP_VERSION") {transitive = false}
  thirdParty("com.google.code.maven-play-plugin.org.allcolor.yahp:yahp-internal:$YAHP_VERSION") {transitive = false}
}

task extractThirdPartyJars(type: Sync) {
  dependsOn configurations.thirdParty

  from {
    configurations.thirdParty.collect { zipTree(it) }
  }
  into "$buildDir/thirdParty/"
  exclude 'META-INF'
  exclude 'META-INF/**/*'
  exclude 'org/xhtmlrenderer/swing/NaiveUserAgent*'
  exclude 'org/allcolor/yahp/cl/converter/CHtmlToPdfFlyingSaucerTransformer*'
}

jar.dependsOn extractThirdPartyJars

jar {
  from sourceSets.main.output + "$buildDir/thirdParty/"
}

apply from: rootProject.file('gradle/deploy.gradle')
